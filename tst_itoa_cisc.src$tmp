// fichier: tst_itoa_cisc.src
// auteur: Alexandre Parodi
// date: 9 mai 2013
// Exemple de de code qui teste la fonction itoa
//
// code C correspondant:
//
// void main(void)
// {
// char text[7];
// int value;
// value = -23;
// itoa(value, text, 10);
// print(text);
// }
//

sp          equ r15
wr          equ r14
bp          equ r13

SP          equ r15
WR          equ r14
BP          equ r13

EXIT_EXC   EQU   64    // n0 d'exception de EXIT
READ_EXC   EQU   65    // n0 d'exception de READ  (lit 1 ligne)
WRITE_EXC  EQU   66    // n0 d'exception de WRITE (affiche texte)

NUL         equ  0     // caracthre NUL qui doit terminer une channe de caracthres
NULL        equ  0     // pointeur nul
NIL         equ  0     // fin de liste channie

STACK_ADRS  equ 0x1000 // adresse de base de pile
LOAD_ADRS   equ 0xFE00 // adresse de chargement du programme

            org LOAD_ADRS
            start main_

// PROGRAMME MAIN supposi giniri automatiquement par le compilateur

// void main(void)
// initialise pointeur de pile sp et pointeur de base d'environnement bp
main_   ldw sp, #STACK_ADRS // pile sera au dessus de STACK_ADRS
        ldw bp, #NIL        // bp initial (fin de liste channie d'environnements)

// { // ouverture du bloc englobant de main
// LNK crie environnement du main pour permettre des variables locales 
// mais sans encore les riserver     
        stw bp, -(sp)
        ldw bp, sp
adi sp, sp, #-2 //allocation de l'espace pour x, diplacement de x : -2
ldw r0, #72
stw r0, (bp)-2

ldw r0, (bp)-2
stw r0, -(sp)
jsr @printval_
adi sp, sp, #2

// }  // fermeture du bloc englobant de main
        ldw sp, bp        // abandonne variables locales de main
        ldw bp, (sp)+     // dipile ancien bp dans bp
        trp #EXIT_EXC     // lance trappe EXIT
        jea @main_        // revient au dibut du main 
                          // si on relance la simulation aprhs EXIT

//============================================================================================

//fonction printval :
printval_ stw bp, -(sp)
          ldw bp, sp
// char toto[7];
   // riserve 7+1 = 8 caracthres en pile
   // (entier pair supirieur ` 7 demandi pour pas disaligner pile)
        adi sp, sp, #-8   // riserve place pour text sur pile (8 octets); 
                          // diplacement du dibut du tableau est -8

// itoa(value, text, 10); // appelle itoa avec i = value, p = text, b = 10

        ldw r0, #10       // charge 10 (pour base dicimale) dans r0
        stw r0, -(sp)     // empile contenu de r0 (paramhtre b)

        adi bp, r0, #-8   // r0 = bp - 8 = adresse du tableau text
        stw r0, -(sp)     // empile contenu de r0 (paramhtre p)

        ldw r0, (bp)4   // charge r0 avec x
        stw r0, -(sp)     // empile contenu de r0 (paramhtre i)

        jsr @itoa_        // appelle fonction itoa d'adresse itoa_

        adi sp, sp, #6    // nettoie la pile des paramhtres 
                          // de taille totale 6 octets

// print(text);

        adi bp, r0, #-8   // r0 = bp - 8 = adresse du tableau text
        stw r0, -(sp)     // empile contenu de r0 (paramhtre p)
        
        jsr @print_       // appelle fonction print d'adresse print_

        adi sp, sp, #2    // nettoie la pile des paramhtres
                          // de taille totale 2 octets
        ldw sp, bp
        ldw bp, (sp)+
        rts


// FONCTIONS PRi-DiFINIES EN LANAGAGE D'ASSEMBLAGE

// char *itoa(int i, char *p, int b);
//
// i entier ` convertir
// p pointeur du tampon dij` alloui en mimoire oy copier la channe de caracthres
// b base de numiration utilisie (de 2 ` 36 inclus car il n'y a que 36 chiffres; par exemple: 2, 4, 8, 10, 16)
//
// Convertit un entier en channe de caracthres codie en ASCII
// (cette fonction fait partie de la bibliothhque standard portable C stdlib et est normalement icrite en C).
// Limitation ici: b doit jtre pair.
// Retourne le pointeur sur la channe de caracthre
//
// Ce programme terminera automatiquement la channe de caracthres par NUL;
// le tampon devrait avoir une taille suffisante (par exemple sizeof(int)*8+1 octets pour b=2)
// Si la base = 10 et que l'entier est nigatif la channe de caracthres est pricidie d'un signe moins (-);
// pour toute autre base, la valeur i est considirie non signie.
// Les 36 chiffres utilisables sont dans l'ordre: 0, 1, 2,..., 9, A, B, C, ... , Z .
// Aucune erreur n'est girie.
     

ITOA_I      equ 4      // offset du paramhtre i
ITOA_P      equ 6      // offset du paramhtre p
ITOA_B      equ 8      // offset du paramhtre b

ASCII_MINUS equ 45     // code ASCII de -
ASCII_PLUS  equ 43     // code ASCII de +
ASCII_SP    equ 32     // code ASCII d'espace SP
ASCII_0     equ 48     // code ASCII de ziro (les autres chiffres jusqu'` 9 suivent dans l'ordre)
ASCII_A     equ 65     // code ASCII de A (les autres lettres jusqu'` Z suivent dans l'ordre alphabitique)

// LNK: crie environnement du main pour permettre des variables locales 
// mais sans encore les riserver
itoa_   stw bp, -(sp)
        ldw bp, sp

// ricupiration des paramhtres depuis pile vers registres
        ldw r0, (bp)ITOA_I    // r0 = i    
        ldw r1, (bp)ITOA_B    // r1 = b
        
// ghre le signe: normalement itoa ghre des int c'est ` dire des entiers signis, 
// mais en fait seulement pour b=10;
// dans ce cas calcule le signe dans r3 et charge r0 avec la valeur absolue de i
        ldq ASCII_SP, r3      // code ASCII de espace (SPace) -> r3
        ldq 10, wr            // 10 -> wr
        cmp r1, wr            // charge les indicateurs de b - 10
        bne NOSIGN-$-2        // si non igal (donc si b != 10) saute en NOSIGN, sinon calcule signe
        ldq ASCII_PLUS, r3    // charge le code ASCII du signe plus + dans r3
        tst r0                // charge les indicateurs de r0 et donc de i
        bge POSIT-$-2         // saute en POSIT si i >= 0
        neg r0, r0            // change le signe de r0
        ldq ASCII_MINUS, r3   // charge le code ASCII du signe moins - dans r3
POSIT   NOP                   // r3 = code ASCII de signe: SP pour aucun, - ou +


// convertit l'entier i en chiffres et les empile de droite ` gauche
NOSIGN  ldw r2, r0            // r2 <- r0
CNVLOOP ldw r0, r2            // r0 <- r2
   
   // effectue "criativement" la division par b supposi pair (car l'instruction div est hilas signie ...)
   // d=2*d' , D = d * q + r  , D = 2*D'+r" , D' = d' * q + r' => r = 2*r'+r"
   // un bug apparant avec SRL R0, R0 avec R0 = 2 : met CF ` 1 !!
        srl r1, r1            // r1 = b/2
        ani r0, r4, #1        // ANd Immidiate entre r0 et 00...01 vers r4:
                              // bit n00 de r0 -> r4; r4 = reste" de r0/2
        srl r0, r0            // r0 / 2 -> r0
        div r0, r1, r2        // quotient = r0 / r1 -> r2, reste' = r0 % r1 -> r0
        shl r0, r0            // r0 = 2 * reste'
        add r0, r4, r0        // r0 = reste = 2 * reste' + reste" => r0 = chiffre
        shl r1, r1            // r1 = b

        adq -10, r0           // chiffre - 10 -> r0 
        bge LETTER-$-2        // saute en LETTER si chiffre >= 10
        adq 10+ASCII_0, r0    // ajoute 10 => r0 = chiffre, ajoute code ASCII de 0 
                              // => r0 = code ASCII de chiffre
        bmp STKCHR-$-2        // saute en STKCHR 

LETTER  adq ASCII_A, r0       // r0 = ASCII(A) pour chiffre = 10, ASCII(B) pour 11 ...
                              // ajoute code ASCII de A => r = code ASCII de chiffre
STKCHR  stw r0, -(sp)         // empile code ASCII du chiffre 
                              // (sur un mot complet pour pas disaligner pile)
        tst r2                // charge les indicateurs en fonction du quotient ds r2)
        bne CNVLOOP-$-2       // boucle si quotient non nul; sinon sort

// les caracthres sont maintenant empilis : gauche en haut et droit en bas

// recopie les caracthres dans le tampon dans le bon ordre: de gauche ` droite
        ldw r1, (bp)ITOA_P    // r1 pointe sur le dibut du tampon dij` alloui 
        stb r3, (r1)+         // copie le signe dans le tampon
CPYLOOP ldw r0, (sp)+         // dipile code du chiffre gauche (sur un mot) dans r0
        stb r0, (r1)+         // copie code du chiffre dans un Byte du tampon de gauche ` droite
        cmp sp, bp            // compare sp et sa valeur avant empilement des caracthres qui itait bp
        bne CPYLOOP-$-2       // boucle s'il reste au moins un chiffre sur la pile
        ldq NUL, r0           // charge le code du caracthre NUL dans r0
        stb r0, (r1)+         // sauve code NUL pour terminer la channe de caracthres

// termine
        ldw r0, (bp)ITOA_P    // retourne le pointeur sur la channe de caracthres

    // UNLINK: fermeture de l'environnement de la fonction itoa
        ldw sp, bp            // sp <- bp : abandonne infos locales; sp pointe sur ancinne valeur de bp
        ldw bp, (sp)+         // dipile ancienne valeur de bp dans bp; sp pointe sur adresse de retour

        rts                   // retourne au programme appelant

//-----------------------------------------------------------------------------------------------------

// void print(char* p)     // imprime le texte pointi par paramhtre p

// pripare l'environnement de la fonction appelie (prologue) :

print_     LDQ 0, R1      // R1 = taille donnies locales (ici 0) de fonction appelie

     // LINK R1        // crie et lie l'environnement de fonction appelie
           STW BP, -(SP)  // empile le contenu du registre BP
           LDW BP, SP     // charge contenu SP ds BP qui pointe sur sa sauvegarde
           SUB SP, R1, SP // riserve R1 octets sur la pile pour la variable locale z

// charge R0 avec le paramhtre p de diplacement 4
           LDW R0, (BP)4  // R0 = M[BP + 4]

// affiche texte pointi par R0
           TRP #WRITE_EXC // lance trappe n0 WRITE_EXC: affiche texte d'adresse R0

// fin de la fonction (ipilogue) :

    // UNLINK 
           LDW SP, BP    // charge SP avec contenu de BP: abandon infos locales
           LDW BP, (SP)+ // dipile ancien BP dans BP

           RTS  // retour au programme appelant
